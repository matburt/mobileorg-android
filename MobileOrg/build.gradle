buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:1.0.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        packageName "com.matburt.mobileorg"
        minSdkVersion 9
        targetSdkVersion 17
        versionCode 913
        versionName getVersionName()
    }

    signingConfigs {
        matburt {
            storeFile file("other.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.matburt
        }
        donate {
            packageNameSuffix ".donate"
            signingConfig signingConfigs.matburt
        }
        debug {
            debuggable true
        }
    }

    packagingOptions { // This is a fix for a bug in the Android gradle plugin v. 0.7.0
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

apply plugin: 'android-unit-test'

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:13.0.+'
    compile 'com.jcraft:jsch:0.1.50'
    testCompile "junit:junit:4+"
    testCompile 'org.robolectric:robolectric:2.3+'
    compile project(':libraries:locale')
    compile fileTree(dir: 'libs', include: '*.jar')

    // These are only here to help IntelliJ find junit and robolectric classes
    androidTestCompile "junit:junit:4+"
    androidTestCompile 'org.robolectric:robolectric:2.3+'
}

