buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:13.0.+'
    compile 'com.jcraft:jsch:0.1.50'
    compile project(':libraries:locale')
    compile fileTree(dir: 'libs', include: '*.jar')
}


def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}





android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 17
        versionCode 913
        versionName getVersionName()
    }

    // Use personl keystore to sign apk's if available
    // ~/.gradle/gradle.properties should look like:
    // customStorePassword = storepassword
    // customKeyAlias = keyalias
    // customKeyPassword = keypassword
    // customStoreFile = /path/to/storefile
    signingConfigs {
        custom {
            if (project.hasProperty('customStoreFile') &&
                project.hasProperty('customStorePassword') &&
                    project.hasProperty('customKeyAlias') &&
                    project.hasProperty('customKeyPassword')) {
                storeFile = file(customStoreFile)
                storePassword = customStorePassword
                keyPassword = customKeyPassword
                keyAlias customKeyAlias
            } else {
                // Use default
                storeFile file("other.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.custom
        }
        donate {
            packageNameSuffix ".donate"
            signingConfig signingConfigs.custom
        }
        debug {
            debuggable true
        }
    }

    packagingOptions { // This is a fix for a bug in the Android gradle plugin v. 0.7.0
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

